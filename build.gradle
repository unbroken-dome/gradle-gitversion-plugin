plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id 'com.gradle.plugin-publish' version '0.9.10'
}

repositories {
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

testSets {
    integrationTest { dirName = 'integration-test' }
}

dependencies {
    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.1.0.202203080745-r'
    implementation 'com.google.guava:guava:31.1-android'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation(platform('org.junit:junit-bom:5.8.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.spockframework:spock-core:2.1-groovy-3.0') {
        exclude module: 'groovy-all'
    }

    integrationTestRuntimeOnly files(pluginUnderTestMetadata.outputDirectory)
}


gradlePlugin {

    plugins {
        gitVersionPlugin {
            id = 'org.unbroken-dome.gitversion'
            implementationClass = 'org.unbrokendome.gradle.plugins.gitversion.GitVersionPlugin'
        }
    }

    testSourceSets project.sourceSets.integrationTest
}

apply from: 'publishing.gradle'
